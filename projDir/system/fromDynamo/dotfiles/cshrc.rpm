##################################################
# .cshrc file for HAWK display installed from RPM
##################################################

if ( $?prompt ) then
  set prompt = "`hostname`:\!>"
  set autoexpand
  set autolist
  set cdpath = ( ~ )
  set pushdtohome
endif

unset autologout
set autologout=0

umask 002
setenv PRINTER lp

# --- RUNTIME ---
#
# projDir is /usr/local/etc/hawk/projDir
#

setenv DATA_HOST pgen
setenv PROJ_DIR /usr/local/etc/hawk/projDir
setenv TITAN_HOME $PROJ_DIR
setenv DATA_DIR $PROJ_DIR/data
setenv RAP_DATA_DIR $DATA_DIR
setenv TMP_DIR $DATA_DIR/tmp

setenv QTDIR /usr/lib/qt4
setenv LD_LIBRARY_PATH $QTDIR/lib:

setenv DS_COMM_TIMEOUT_MSECS 60000
setenv DS_PING_TIMEOUT_MSECS 20000

# --- site info --- 

if (-f $PROJ_DIR/system/params/site_info) then
  source $PROJ_DIR/system/params/site_info
endif

# --- Path ---

# Assemble path to include all scripts directories
# then projDir/bin, then the rest

set path = (. $path)
if ( -d $PROJ_DIR ) then
  foreach dir ($PROJ_DIR/*)
    if (-d $dir/scripts) then
      set path = ($path $dir/scripts)
    endif
  end
endif

set path = ($path ~/bin \
        /usr/local/bin /opt/local/bin \
        /usr/X11R6/bin /usr/sbin /usr/bin /sbin /bin \
	/usr/bin/X11 /usr/local/bin /usr/local/sbin \
	/opt/gfortran/irun/bin /opt/spol/bin \
        /usr/sigmet/bin /usr/sigmet/bin/rda $QTDIR/bin )

# --- Aliases ---
if ($?prompt) then

  alias sc source ~/.cshrc

  alias setprompt 'set prompt="(`hostname`) `basename $cwd` ! % "'
  alias cd 'cd \!*;setprompt; echo $cwd'
  alias push 'pushd \!*; cd .'
  alias pop 'popd; cd .'

  set prompt = "%m:%~%# "             # tcsh
  set showdots                        # tcsh
  set watch = (1 root any)            # tcsh
  set history=1000
  set nobeep
  set time = 5

  setenv EDITOR emacs
  setenv PAGER less
  setenv LESS "-cMe"

  alias h history
  alias ssh 'ssh -X'

  alias rm 'rm -i'
  alias mv 'mv -i'
  alias cp 'cp -i' 
  alias df 'df -h'
  set filec
  set fignore = (.o .ln)

  alias catw 'cat `which \!*`'
  alias lessw 'less `which \!*`'
  alias emacsw 'emacs -nw `which \!*`'
  alias viw 'vi `which \!*`'

  alias lst 'ls -l \!* | awk \{s+=\$4\}END\{print\ s\}'
  alias la 'ls -la --color=tty'
  alias ll 'ls -l --color=tty'
  alias lt 'ls -lt --color=tty'
  alias lat 'ls -alt --color=tty'
  alias l 'ls -sF'
  alias L ls -agl
  alias LS ls -aFRts 

  alias PrintDataMap 'PrintDataMap -all -relt -lreg'
  alias ppm 'print_procmap -hb -up -status'
  alias pdm 'PrintDataMap -all -relt -lreg'
  alias pcheck 'procmap_list_check -proc_list $PROJ_DIR/control/proc_list'
  alias ph 'get_host_type'

  setprompt

endif

